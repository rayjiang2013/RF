# -*- coding: robot -*-
| *Setting* | *Value *|
| Documentation | Configure FSW via a telnet Console, then configure device with static data file |
| Default Tags | Static-Configure-FSW |
| Resource | ${autopath}/cfg/${testbed}/duts.txt |
| Resource | ${autopath}/lib/fsw/telnet.txt |
| Resource | ${autopath}/lib/fsw/util.txt |
| Test Timeout | 20 minutes |

| *Variables*  | *Value* |
| ${dut_name} | ${${dut_id}.name} |
| ${dut_username} | ${${dut_id}.username} |
| ${dut_password} | ${${dut_id}.password} |
| ${dut_termip} | ${${dut_id}.termip} |
| ${dut_termport} | ${${dut_id}.termport} |
| ${config_data} | "config system global\nend" |
| ${telnet_alias} | 'telnetconsole' |
| ${show} | "no" |

| *Test Cases*  | *Value* | *Value* | *Value* |
| Configure Device | [Documentation] | Configure Device With static data file |
| | Show Test Related Variables |
| | ${config_data}= | Get File | ${autopath}/cfg/${testbed}/${configFile} |
| | Should Not Be Empty | ${config_data} | "cfg_info.txt cannot be empty" |
| | ${dut_prompt}= | Telnet Login | ${dut_termip} | ${telnet_alias} | ${dut_username} | ${dut_password} | 10 | ${dut_name} | ${dut_termport} | open=open | send_ctrlc=yes |
| | ${ret}= | Get Lines Containing String | ${dut_prompt} | ${dut_name} |
| | Should Match Regexp | ${ret} | ${dut_name} | "Telnet Login to ${dut_termip} failed" |
| | Configure FortiSwitch | ${config_data} |
| | Sleep | 2 |
| | Run Keyword If | "${show}" == "yes" | Show Switch | ${dut_name} | ${telnet_alias} |
| | [Teardown] | Close All Connections |

| *Keywords* |
| Show Test Related Variables | [Documentation] | Show all variables using in this test, autopath and dut_id are passed via command line |
| | Log To Console | \n\tautopath=${autopath} |
| | Log To Console | \tdut_id=${dut_id} |
| | Log To Console | \tdut_name=${dut_name} |
| | Log To Console | \tdut_username=${dut_username} |
| | Log To Console | \tdut_password=${dut_password} |
| | Log To Console | \tdut_termip=${dut_termip} |
| | Log To Console | \tdut_termport=${dut_termport} |

| Configure FortiSwitch | [Arguments] | ${config_data} |
| | [Documentation] | Configure FortiSwitch |
| | @{config_data_list} | Split to lines | ${config_data} |
| | :FOR | ${line} | IN | @{config_data_list} |
| | | Telnet.Write | ${line} |
| | | Sleep | 1 |
| | | ${read_info}= | Telnet.Read |
| | | Log To Console | ${read_info}${line} |
